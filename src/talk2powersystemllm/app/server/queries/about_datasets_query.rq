PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX onto: <http://www.ontotext.com/>
PREFIX dct: <http://purl.org/dc/terms/>

SELECT DISTINCT ?uri ?name ?date
FROM onto:explicit {
    ?uri a dcat:Dataset
    OPTIONAL {
        SELECT ?uri (SAMPLE(SUBSTR(STR(?dateTime), 1, 10)) AS ?date) {
            ?uri a dcat:Dataset;
            dct:issued ?dateTime
        }
        GROUP BY ?uri
    }
    OPTIONAL {
        SELECT ?uri (SAMPLE(?t) AS ?title) {
            ?uri a dcat:Dataset;
            dct:title ?t
        }
        GROUP BY ?uri
    }
    OPTIONAL {
        SELECT ?uri (SAMPLE(?d) AS ?description) {
            ?uri a dcat:Dataset;
            dct:description ?d.
            FILTER(LANG(?d) != "no")
        }
        GROUP BY ?uri
    }
    BIND(IF(BOUND(?title), CONCAT(?title, IF(BOUND(?description), ": ", ""), ?description), ?description) AS ?name)
}
ORDER BY ?uri
