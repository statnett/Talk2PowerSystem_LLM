PREFIX onto: <http://www.ontotext.com/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

SELECT ?uri ?name ?version ?date
FROM onto:explicit {
    ?uri a owl:Ontology.
    OPTIONAL {
        SELECT ?uri (SAMPLE(?t) AS ?title) {
            ?uri a owl:Ontology;
            dct:title ?t
        }
        GROUP BY ?uri
    }
    OPTIONAL {
        SELECT ?uri (SAMPLE(?l) AS ?label) {
            ?uri a owl:Ontology;
            rdfs:label ?l
        }
        GROUP BY ?uri
    }
    OPTIONAL {
        SELECT ?uri (SAMPLE(?v) AS ?version) {
            ?uri a owl:Ontology;
            owl:versionInfo ?v
        }
        GROUP BY ?uri
    }
    OPTIONAL {
        SELECT ?uri (SAMPLE(?d) AS ?date) {
            ?uri a owl:Ontology;
            dct:modified ?d
        }
        GROUP BY ?uri
    }
    BIND(COALESCE(?title, ?label) AS ?name)
}
ORDER BY ?uri
